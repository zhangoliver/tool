#!/usr/bin/expect --

# \file Name: to
# Created: Fang lungang 12/26/2008
# Modified:Kevin Zhang 04/29/2014 15:58>

# \brief This is a re-write of tn in TCL/expect

# lgfang: modify this accordingly
source /home/oliverz/lib/cmdline.tcl
# seems 'source' instead of 'package require' makes it faster

# set TCLLIB_PATH "/home/lungangfang/local/lib/tcllib1.11"
# lappend auto_path $TCLLIB_PATH
# package require cmdline

set nul \0;                     # according to `exploring expect', it should not
                                # work. But it works
set esc \033
set host {}
set hostname {}
set username root;              # default username
set password yt_xk39b;        # default password
set commands {}
set tl1Port 2361;               # plexus specific
set reTry 1

set synopsis {
    Usage: to [-a] [-q|-f|-t|-s] 'host_nick_name'

    host: The regexp which matches part of name/IP of destination
    host.  The single quotes may be ommitted if no file name in PWD
    matches the regexp.

    for example, to telnet to TL1 of the lab named summer and act-user
    automatically, you can:

    to -a -t sum

    *NOTE* however: due to limit of ::cmdline::getoption, you can't abbrev above
    example as:

    to -at sum

    the translation from nick name to host address is done based on "~/.hosts".
    ".hosts" may contain one field, or 2 fields, or 3 fileds or more. If
    presented:

    Field 1 ==> host address
    Field 2 ==> host nick name
    Field 3 ==> login for that host (default is "root")
    Field 4 ==> password (default is "plexus9000"), be cautious to put
                password in your .hosts. Other ones might able read it.
    Field 5 etc. ==> command to be executed after login

    ".hosts" example:

    # a record with addr only
    gdcsr60.gdc.lucent.com
    # a record with ip, nick name, login
    135.252.41.248 qdbuild2 lungangfang
    # a record with ip, nick name, login and passwd
    135.252.135.68 qdi02-c04 root newsys
    # a record which will run a command after login
    135.252.152.115 loads public public send \r; send "cd /home/toaster/LCP \r"
    # login to a sever via an intermediate host, like 'ssh -t middle ssh dest'
    152.148.163.134 wf03-c01 root telica send \r; send "ssh 152.148.184.200\r"
}

set options {
    {q  "query host address only"}
    {a  "auto login, input username/password for you"}
    {t  "telnet to TL1"}
    {f  "ftp to the server"}
    {s  "ssh to server"}
    {x  "sftp to server"}
    { i.arg "~/.hosts"  "input file, ~/.hosts if not specified"}
}

trap SIG_DFL SIGINT

proc getHostInfo {target {query false} } {
    global hostfile host hostname username password commands

    set grep_cmd [concat {grep -v '[ \t]*#'} \
                      " $hostfile | grep -i $target | " \
                      {sed 's/[ \t][ \t]*/ /g'}]
    # or " $hostfile | grep -iw $target | " ?
    # sed line is for split later

    if { [catch {set entries [split [exec bash -c $grep_cmd ] "\n"]}] } {
        send_user "grep fail, please contact author\n"
        exit -3
    }

    if { ! [llength $entries] } {
        send_user "no match for $target\n"
        exit -4
    }

    set x 1
    foreach match $entries {
        send_user "$x,\t[lrange $match 0 1]\n"
        incr x
    }

    if { $query } {exit 0}

    set choice "X"
    if { [expr [llength $entries] > 1] } {
        set timeout -1
        while { ! [string is integer $choice] || $choice > [llength $entries]} {
            send "Make a choice (1): "
            expect -re ".*\n"
            set choice [string trim $expect_out(buffer)]

            if { [string length $choice] == 0 } {
                set choice 1
            }
        }

        incr choice -1
    } else {
        set choice 0
    }

    set fields [split [string trim [lindex $entries $choice ]]]

    set fieldNum [llength $fields]
    if { $fieldNum >= 1 } {
        set host [lindex $fields 0]
    }

    if { $fieldNum >= 2 } {
        set hostname [lindex $fields 1]
    } else {
        set hostname $host
    }

    if { $fieldNum >= 3 } {
            set username [lindex $fields 2]
    }

    if { $fieldNum >= 4 } {
            set password [lindex $fields 3]
    }
    if { $fieldNum >= 5 } {
            set commands [lrange $fields 4 end]
    }
}

proc auto_telnet { } {
    global username password commands
    set timeout 10
    expect -re "(user name|login|Login):"
    send -- "$username\r"
    expect {
        -re "\[Pp\]assword:" {send -- "$password\r"}
        # for plex labs in case need no password and suppose PS1 is hostname#
        -re ".*#" {send "\r"}
    }
    if { $commands != {} } {
        expect {
            -re "." {
                eval "[join $commands]"
            }
        }
    }
}

proc auto_ftp {} {
    global username password
    set timeout 30
    send_user "auto_ftp\n"
    expect -re "Name (.*):"
    send -- "$username\r"
    expect -re "Password:"
    send -- "$password\r"
}

proc auto_ssh {} {
    global username password commands reTry
    set timeout 60
    expect {
        timeout {exit}
        "continue connecting (yes/no)?" {
            send "yes\r"
            exp_continue
        }
        -re "\[Pp]assword:" {
            send -- "$password\r"
        }
        "Host key verification failed." {
            expect_after;       # do not exit on eof
            if {[regexp -line "^Offending key in.*\.ssh/known_hosts:(\[0-9]*)" \
                     $expect_out(buffer) dummy lineNum] ==1} { 
                send_user "Delete stored finger print? (n/y) ";
                expect_user {
                    -re "\[yY]" {exec sed -i "${lineNum}d" [glob "~/.ssh/known_hosts"]}
                    -re "\[^Yy]" {}
                }
                # exit 0
                return $reTry
            } 
        }
    }

    if { $commands != {} } {
        expect {
            -re ">" {
                eval "[join $commands]"
            }
        }
    }
}

proc auto_tl1 {} {
    # this is a plexus specific function

    set tl1Tries [list "telica" "plexntmctrl" "plexview" "plexview1"]
    set timeout 30
    global esc

    expect {
        timeout {send_user "timeout\n"; return }
        "Connection refused" {return}
        "Escape character is '^]'" { send "${esc}" }
    }

    expect ">"
    foreach USERNAME $tl1Tries {
        # password is same as user name
        set PASSWORD $USERNAME
        send "act-user::${USERNAME}:::${PASSWORD};"
        expect {
            timeout { send_user "Timed out Waiting for COMPLD\n"; exit -1}
            "DENY" { send_user "try $USERNAME\n";  }
            "COMPLD" {break}
        }
    }
}


# main
if { [catch {array set params \
                 [::cmdline::getoptions argv $options $synopsis]} res] } {
    send_user stderr $res
    exit -1
}

if { [llength $argv] != 1 } {
    send_user stderr [::cmdline::usage $options $synopsis]
    exit -2
}

set query false
if { $params(q) } {set query true}
set hostfile $params(i)
if {! [file exists $hostfile] } {
    send_user "Erro: can't access $hostfile\n"
    exit -1
}

getHostInfo [lindex $argv 0] $query

if { $params(f) } {
    set application ftp
    spawn $application $host
} elseif { $params(s) } {
    set application ssh
    if { $params(a) } {
        spawn $application $username@$host
    } else {
        #change by oliver
        spawn $application $username@$host
    }
} elseif { $params(t) } { # login tl1
    set application tl1
    spawn telnet $host $tl1Port
} elseif { $params(x) } {
    set application sftp
    if { $params(a) } {
        spawn $application $username@$host
    } else {
        spawn $application $host
    }
} else {
    #change by oliver
    set application ssh
    spawn $application $username@$host
}

if {[info exists ::env(TMUX)]} {
    exec tmux rename-window $hostname
}

# expect *AFTER* to allow dealing with what we get right before "eof"
expect_after eof {              
    if {[info exists ::env(TMUX)]} {
        exec tmux set-window-option automatic-rename on
    }
    send_user "connection closed\n"
    exit 0
}

if { $params(a) } {
    if {[string compare $application sftp] == 0} {
        set appAuto ssh
    } else {
        set appAuto $application
    }

    if {[eval auto_$appAuto] == $reTry} {
        spawn $application $username@$host

        if {[info exists ::env(TMUX)]} {
            exec tmux rename-window $hostname
        }

        # expect *AFTER* to allow dealing with what we get right before "eof"
        expect_after eof {              
            if {[info exists ::env(TMUX)]} {
                exec tmux set-window-option automatic-rename on
            }
            send_user "connection closed\n"
            exit 0
        }

        eval auto_$appAuto
    }
}

interact {
    # add your own "hot-keys" here
    # "${esc}OP" -> F1
    # "${esc}OQ" -> F2
    # "${esc}OR" -> F3
    # "${esc}OS" -> F4
    "${esc}OPtl1" {send "telnet 0 $tl1Port\r"; auto_tl1}
    "${esc}OPcu" {send "CANC-USER;"}
    "${esc}OPrs" {send "RTRV-ATCA-EQPTSTATE;"}
    "${esc}OPr1" {send "RTRV-ATCA-EQPT::SHELF-0-ACM-1;"}
    "${esc}OPr2" {send "RTRV-ATCA-EQPT::SHELF-0-ACM-2;"}
    "${esc}OPr3" {send "RTRV-ATCA-EQPT::SHELF-0-ACM-3;"}
    "${esc}OPr4" {send "RTRV-ATCA-EQPT::SHELF-0-ACM-4;"}
    "${esc}OPr4" {send "RTRV-ATCA-EQPT::SHELF-0-ACM-4;"}
    # NUL bug in expect 5.43, fixed in expect 5.45?
    "${nul}" { send "${nul}"}

    # TODO: privide capability to search input history
}

if {[info exists ::env(TMUX)]} {
    exec tmux set-window-option automatic-rename on
}
